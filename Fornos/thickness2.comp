#version 430 core
#extension GL_ARB_compute_shader : enable
#extension GL_ARB_shader_storage_buffer_object : enable

layout (local_size_x = 32) in;

#define BUFFER_PARAMS 3
#define BUFFER_RAYS 4
#define BUFFER_SAMPLES 5
#define BUFFER_VERTICES 6
#define BUFFER_MESHDIVS 7
#define BUFFER_RESULTS 8

#define FLT_MAX 3.402823466e+38

struct Params
{
	uint sampleCount; // Number of rays to sample
	uint meshDivsCount;
	float minDistance;
	float maxDistance;
};

struct Ray
{
	float x, y, z;
	float i, j, k;
};

struct MeshDiv
{
	float x, y, z;
	float w, h, d;
	uint start;
	uint end;
};

struct RaySample { float x, y, z; };
struct Point { float x, y, z; };

layout(location = 1) uniform uint workOffset;
layout(std430, binding = BUFFER_PARAMS) readonly buffer paramsBuffer { Params params; };
layout(std430, binding = BUFFER_RAYS) readonly buffer orgBuffer { Ray rays[]; };
layout(std430, binding = BUFFER_SAMPLES) readonly buffer samplesBuffer { RaySample samples[]; };
layout(std430, binding = BUFFER_VERTICES) readonly buffer trisBuffer { Point vertices[]; };
layout(std430, binding = BUFFER_MESHDIVS) readonly buffer meshDivsBuffer { MeshDiv divisions[]; };
layout(std430, binding = BUFFER_RESULTS) writeonly buffer resultBuffer { float results[]; };



bool RayAABB(vec3 o, vec3 d, vec3 c, vec3 s)
{
	vec3 mins = c - s;
	vec3 maxs = c + s;
	vec3 dabs = abs(d);
	if (dabs.x < 0.0001) d.x = 0.0001;
	if (dabs.y < 0.0001) d.y = 0.0001;
	if (dabs.z < 0.0001) d.z = 0.0001;
	vec3 t1 = (mins - o) / d;
	vec3 t2 = (maxs - o) / d;
	vec3 tmin = min(t1, t2);
	vec3 tmax = max(t1, t2);
	float a = max(tmin.x, max(tmin.y, tmin.z));
	float b = min(tmax.x, min(tmax.y, tmax.z));
	return (b >= 0 && a <= b);
}

vec3 barycentric(vec3 p, vec3 a, vec3 b, vec3 c)
{
	vec3 v0 = b - a;
	vec3 v1 = c - a;
	vec3 v2 = p - a;
	float d00 = dot(v0, v0);
	float d01 = dot(v0, v1);
	float d11 = dot(v1, v1);
	float d20 = dot(v2, v0);
	float d21 = dot(v2, v1);
	float denom = d00 * d11 - d01 * d01;
	if (abs(denom) < 0.0001) return vec3(-1,-1,-1);
	float y = (d11 * d20 - d01 * d21) / denom;
	float z = (d00 * d21 - d01 * d20) / denom;
	return vec3(1.0 - y - z, y, z);
}

float raycast(vec3 o, vec3 d, vec3 a, vec3 b, vec3 c)
{
	vec3 n = normalize(cross(b - a, c - a));
	float nd = dot(d, n);
	if (abs(nd) > 0.0001f)
	{
		float pn = dot(o, n);
		float t = (dot(a, n) - pn) / nd;
		if (t >= 0.0f)
		{
			vec3 p = o + d * t;
			vec3 b = barycentric(p, a, b, c);
			if (b.x >= 0 && b.x <= 1 && 
				b.y >= 0 && b.y <= 1 && 
				b.z >= 0 && b.z <= 1)
			{
				return t;
			}
		}
	}
	return -1;
}

float raycastRange(vec3 o, vec3 d, uint start, uint end, float mindist)
{
	float mint = FLT_MAX;

	for (uint tidx = start; tidx < end; tidx += 3)
	{
		Point p0 = vertices[tidx + 0];
		Point p1 = vertices[tidx + 1];
		Point p2 = vertices[tidx + 2];
		vec3 v0 = vec3(p0.x, p0.y, p0.z);
		vec3 v1 = vec3(p1.x, p1.y, p1.z);
		vec3 v2 = vec3(p2.x, p2.y, p2.z);
		float t = raycast(o, d, v0, v1, v2);
		if (t >= mindist)
		{
			mint = min(t, mint);
		}
	}

	return (mint != FLT_MAX) ? mint : -1;
}

void main()
{
	uint gid = gl_GlobalInvocationID.x + workOffset;

	Ray ray = rays[gid];
	vec3 o = vec3(ray.x, ray.y, ray.z);
	vec3 d0 = vec3(ray.i, ray.j, ray.k);

	vec3 up = abs(d0.y) < 0.99 ? vec3(0,1,0) : vec3(0,0,-1);
	vec3 tx = normalize(cross(up, d0));
	vec3 ty = cross(d0, tx);

	float rAcc = 0;
	float rCount = 0;

	for (uint sidx = 0; sidx < params.sampleCount; ++sidx)
	{
		RaySample rs = samples[sidx];
		vec3 d = normalize(tx * rs.x + ty * rs.y + d0 * rs.z);
		float tmin = FLT_MAX;

		for (uint dividx = 0; dividx < params.meshDivsCount; ++dividx)
		{
			MeshDiv division = divisions[dividx];
			if (RayAABB(o, d, vec3(division.x, division.y, division.z), vec3(division.w, division.h, division.d)))
			{
				float t = raycastRange(o, d, division.start, division.end, params.minDistance);
				if (t > 0.0)
				{
					tmin = min(t, tmin);
				}
			}
		}

		if (tmin != FLT_MAX)
		{
			rAcc += min(tmin, params.maxDistance);
			rCount += rs.z;
		}
	}

	results[gid] = rCount > 0 ? rAcc / rCount : 0;
	//results[gid] = rAcc;
}