#version 430 core
#extension GL_ARB_compute_shader : enable
#extension GL_ARB_shader_storage_buffer_object : enable

layout (local_size_x = 64) in;

#define FLT_MAX 3.402823466e+38
#define BARY_MIN -1e-3
#define BARY_MAX 1.0

struct Pix
{
	vec3 p;
	vec3 n;
};

struct BVH
{
	float aabbMinX; float aabbMinY; float aabbMinZ;
	float aabbMaxX; float aabbMaxY; float aabbMaxZ;
	uint start;
	uint end;
	uint jump; // Index to the next BVH if we skip this subtree
};

layout(location = 1) uniform uint workOffset;
layout(location = 2) uniform uint workCount;
layout(location = 3) uniform uint bvhCount;
layout(std430, binding = 4) readonly buffer pixBuffer { Pix pixels[]; };
layout(std430, binding = 5) readonly buffer meshPBuffer { vec3 positions[]; };
layout(std430, binding = 6) readonly buffer bvhBuffer { BVH bvhs[]; };
layout(std430, binding = 7) writeonly buffer rCoordBuffer { vec4 r_coords[]; };
layout(std430, binding = 8) writeonly buffer rTidxBuffer { uint r_tidx[]; };

float RayAABB(vec3 o, vec3 d, vec3 mins, vec3 maxs)
{
	vec3 dabs = abs(d);
	vec3 t1 = (mins - o) / d;
	vec3 t2 = (maxs - o) / d;
	vec3 tmin = min(t1, t2);
	vec3 tmax = max(t1, t2);
	float a = max(tmin.x, max(tmin.y, tmin.z));
	float b = min(tmax.x, min(tmax.y, tmax.z));
	return (b >= 0 && a <= b) ? a : FLT_MAX;
}

vec3 barycentric(vec3 p, vec3 a, vec3 b, vec3 c)
{
	vec3 v0 = b - a;
	vec3 v1 = c - a;
	vec3 v2 = p - a;
	float d00 = dot(v0, v0);
	float d01 = dot(v0, v1);
	float d11 = dot(v1, v1);
	float d20 = dot(v2, v0);
	float d21 = dot(v2, v1);
	float denom = d00 * d11 - d01 * d01;
	float y = (d11 * d20 - d01 * d21) / denom;
	float z = (d00 * d21 - d01 * d20) / denom;
	return vec3(1.0 - y - z, y, z);
}

vec4 raycast(vec3 o, vec3 d, vec3 a, vec3 b, vec3 c)
{
	vec3 n = normalize(cross(b - a, c - a));
	float nd = dot(d, n);
	if (nd > 0.0f)
	{
		float pn = dot(o, n);
		float t = (dot(a, n) - pn) / nd;
		if (t >= 0.0f)
		{
			vec3 p = o + d * t;
			vec3 b = barycentric(p, a, b, c);
			if (b.x >= BARY_MIN && b.y >= BARY_MIN && b.y <= BARY_MAX && b.z >= BARY_MIN && b.z <= BARY_MAX)
			{
				return vec4(t, b.x, b.y, b.z);
			}
		}
	}
	return vec4(FLT_MAX, 0, 0, 0);
}

float raycastRange(vec3 o, vec3 d, uint start, uint end, float mindist, out uint o_idx, out vec3 o_bcoord)
{
	float mint = FLT_MAX;
	for (uint tidx = start; tidx < end; tidx += 3)
	{
		vec3 v0 = positions[tidx + 0];
		vec3 v1 = positions[tidx + 1];
		vec3 v2 = positions[tidx + 2];
		vec4 r = raycast(o, d, v0, v1, v2);
		if (r.x >= mindist && r.x < mint)
		{
			mint = r.x;
			o_idx = tidx;
			o_bcoord = r.yzw;
		}
	}
	return mint;
}

float raycastBVH(vec3 o, vec3 d, float mint, in out uint o_idx, in out vec3 o_bcoord)
{
	uint i = 0;
	while (i < bvhCount)
	{
		BVH bvh = bvhs[i];
		vec3 aabbMin = vec3(bvh.aabbMinX, bvh.aabbMinY, bvh.aabbMinZ);
		vec3 aabbMax = vec3(bvh.aabbMaxX, bvh.aabbMaxY, bvh.aabbMaxZ);
		float distAABB = RayAABB(o, d, aabbMin, aabbMax);
		if (distAABB < mint)
		{
			uint ridx = 0;
			vec3 rbcoord = vec3(0, 0, 0);
			float t = raycastRange(o, d, bvh.start, bvh.end, 0, ridx, rbcoord);
			if (t < mint)
			{
				mint = t;
				o_idx = ridx;
				o_bcoord = rbcoord;
			}
			++i;
		}
		else
		{
			i = bvh.jump;
		}
	}

	return mint;
}

vec4 raycastBack(vec3 o, vec3 d, vec3 a, vec3 b, vec3 c)
{
	vec3 n = normalize(cross(b - a, c - a));
	float nd = dot(d, n);
	if (nd < 0.0f)
	{
		float pn = dot(o, n);
		float t = (dot(a, n) - pn) / nd;
		if (t >= 0.0f)
		{
			vec3 p = o + d * t;
			vec3 b = barycentric(p, a, b, c);
			if (b.x >= BARY_MIN && b.y >= BARY_MIN && b.y <= BARY_MAX && b.z >= BARY_MIN && b.z <= BARY_MAX)
			{
				return vec4(t, b.x, b.y, b.z);
			}
		}
	}
	return vec4(FLT_MAX, 0, 0, 0);
}

float raycastBackRange(vec3 o, vec3 d, uint start, uint end, float mindist, out uint o_idx, out vec3 o_bcoord)
{
	float mint = FLT_MAX;
	for (uint tidx = start; tidx < end; tidx += 3)
	{
		vec3 v0 = positions[tidx + 0];
		vec3 v1 = positions[tidx + 1];
		vec3 v2 = positions[tidx + 2];
		vec4 r = raycastBack(o, d, v0, v1, v2);
		if (r.x >= mindist && r.x < mint)
		{
			mint = r.x;
			o_idx = tidx;
			o_bcoord = r.yzw;
		}
	}
	return mint;
}

float raycastBackBVH(vec3 o, vec3 d, float mint, in out uint o_idx, in out vec3 o_bcoord)
{
	uint i = 0;
	while (i < bvhCount)
	{
		BVH bvh = bvhs[i];
		vec3 aabbMin = vec3(bvh.aabbMinX, bvh.aabbMinY, bvh.aabbMinZ);
		vec3 aabbMax = vec3(bvh.aabbMaxX, bvh.aabbMaxY, bvh.aabbMaxZ);
		float distAABB = RayAABB(o, d, aabbMin, aabbMax);
		if (distAABB < mint)
		{
			uint ridx = 0;
			vec3 rbcoord = vec3(0, 0, 0);
			float t = raycastBackRange(o, d, bvh.start, bvh.end, 0, ridx, rbcoord);
			if (t < mint)
			{
				mint = t;
				o_idx = ridx;
				o_bcoord = rbcoord;
			}
			++i;
		}
		else
		{
			i = bvh.jump;
		}
	}

	return mint;
}

void main()
{
	uint gid = gl_GlobalInvocationID.x + workOffset;
	if (gid >= workCount) return;

	Pix pix = pixels[gid];
	vec3 p = pix.p;
	vec3 n = pix.n;

	uint tidx = 4294967295;
	vec3 bcoord = vec3(0, 0, 0);
	float t = raycastBVH(p, n, FLT_MAX, tidx, bcoord);
	t = min(t, raycastBackBVH(p, -n, t, tidx, bcoord));

	r_coords[gid] = vec4(t, bcoord.x, bcoord.y, bcoord.z);
	r_tidx[gid] = tidx;
}
